\section{43. Using filetypes}
When you are editing a file of a certain type, for example a C program or a shell script, you often use the same option settings and mappings.
You quickly get tired of manually setting these each time.
This chapter explains how to do it automatically.
\localtableofcontentswithrelativedepth{+1}
\subsection{Plugins for a filetype}            
\label{filetype-plugin}
How to start using filetype plugins has already been discussed here: \hyperref[add-filetype-plugin]{|\texttt{add-filetype-plugin}|}.
But you probably are not satisfied with the default settings, because they have been kept minimal.
Suppose that for C files you want to set the 'softtabstop' option to 4 and define a mapping to insert a three-line comment.
You do this with only two steps:

\phantomsection                            
\label{your-runtime-dir}
\begin{enumerate}
				\item Create your own runtime directory.
								On Unix this usually is "\verb!~/.vim!".
								In this directory create the "\verb!ftplugin!" directory:

								\begin{Verbatim}[samepage=true]
	mkdir ~/.vim
	mkdir ~/.vim/ftplugin
								\end{Verbatim}


								When you are not on Unix, check the value of the 'runtimepath' option to see where Vim will look for the "\verb!ftplugin!" directory:

								\begin{Verbatim}[samepage=true]
 set runtimepath
								\end{Verbatim}

								You would normally use the first directory name (before the first comma).

								You might want to prepend a directory name to the 'runtimepath' option in your |\verb!:h vimrc!| file if you don't like the default value.

				\item Create the file "\verb!~/.vim/ftplugin/c.vim!", with the contents:
								\begin{Verbatim}[samepage=true]
	setlocal softtabstop=4
	noremap <buffer> <LocalLeader>c o/**************<CR><CR>/<Esc>
								\end{Verbatim}
\end{enumerate}

Try editing a C file.
You should notice that the 'softtabstop' option is set to 4.
But when you edit another file it's reset to the default zero.
That is because the "\verb!:setlocal!" command was used.
This sets the 'softtabstop' option only locally to the buffer.
As soon as you edit another buffer, it will be set to the value set for that buffer.
For a new buffer it will get the default value or the value from the last "\verb!:set!" command.

Likewise, the mapping for "\verb!\c!" will disappear when editing another buffer.
The "\verb!:map <buffer>!" command creates a mapping that is local to the current buffer.
This works with any mapping command: "\verb!:map!!", "\verb!:vmap!", etc.
The |\verb!:h <LocalLeader>!| in the mapping is replaced with the value of the "\verb!maplocalleader!" variable.

You can find examples for filetype plugins in this directory:

\begin{Verbatim}[samepage=true]
 $VIMRUNTIME/ftplugin/
\end{Verbatim}

More details about writing a filetype plugin can be found here: \hyperref[write-plugin]{|\texttt{write-plugin}|}.
\subsection{Adding a filetype}
\label{Adding a filetype}
If you are using a type of file that is not recognized by Vim, this is how to get it recognized.
You need a runtime directory of your own.
See \hyperref[your-runtime-dir]{|\texttt{your-runtime-dir}|} above.

Create a file "\verb!filetype.vim!" which contains an autocommand for your filetype.
(Autocommands were explained in section |\hyperref[Autocommands]{\texttt{Autocommands}}|.)
Example:

\begin{Verbatim}[samepage=true]
 augroup filetypedetect
 au BufNewFile,BufRead *.xyz setf xyz
 augroup END
\end{Verbatim}

This will recognize all files that end in "\verb!.xyz!" as the "\verb!xyz"! filetype.
The "\verb!:augroup!" commands put this autocommand in the "\verb!filetypedetect!" group.
This allows removing all autocommands for filetype detection when doing "\verb!:filetype off!".
The "\verb!setf!" command will set the 'filetype' option to its argument, unless it was set already.
This will make sure that 'filetype' isn't set twice.

You can use many different patterns to match the name of your file.
Directory names can also be included.
See |\verb!:h autocmd-patterns!|.
For example, the files under "\verb!/usr/share/scripts/!" are all "\verb!ruby!" files, but don't have the expected file name extension.
Adding this to the example above:

\begin{Verbatim}[samepage=true]
 augroup filetypedetect
 au BufNewFile,BufRead *.xyz                 setf xyz
 au BufNewFile,BufRead /usr/share/scripts/*  setf ruby
 augroup END
\end{Verbatim}

However, if you now edit a file /usr/share/scripts/README.txt, this is not a
ruby file.  The danger of a pattern ending in "\verb!*!" is that it quickly matches
too many files.  To avoid trouble with this, put the filetype.vim file in
another directory, one that is at the end of 'runtimepath'.  For Unix for
example, you could use "\verb!~/.vim/after/filetype.vim!".

You now put the detection of text files in ~/.vim/filetype.vim:

\begin{Verbatim}[samepage=true]
 augroup filetypedetect
 au BufNewFile,BufRead *.txt         setf text
 augroup END
\end{Verbatim}

That file is found in 'runtimepath' first.
Then use this in \verb!~/.vim/after/filetype.vim!, which is found last:

\begin{Verbatim}[samepage=true]
 augroup filetypedetect
 au BufNewFile,BufRead /usr/share/scripts/*  setf ruby
 augroup END
\end{Verbatim}

What will happen now is that Vim searches for "\verb!filetype.vim!" files in each directory in 'runtimepath'.
First \verb!~/.vim/filetype.vim! is found.
The autocommand to catch \verb!*.txt! files is defined there.
Then Vim finds the filetype.vim file in \verb!$VIMRUNTIME!, which is halfway 'runtimepath'.
Finally \verb!/.vim/after/filetype.vim! is found and the autocommand for detecting ruby files in \verb!/usr/share/scripts! is added.

When you now edit \verb!/usr/share/scripts/README.txt!, the autocommands are checked in the order in which they were defined.
The \verb!*.txt! pattern matches, thus "\verb!setf text!" is executed to set the filetype to "\verb!text!".
The pattern for ruby matches too, and the "\verb!setf ruby!" is executed.
But since 'filetype' was already set to "\verb!text!", nothing happens here.

When you edit the file \verb!/usr/share/scripts/foobar! the same autocommands are checked.
Only the one for ruby matches and "\verb!setf ruby!" sets 'filetype' to ruby.

\subsubsection{Recognizing by contents}
If your file cannot be recognized by its file name, you might be able to recognize it by its contents.
For example, many script files start with a line like:

\begin{Verbatim}[samepage=true]
    #!/bin/xyz 
\end{Verbatim}

To recognize this script create a file "\verb!scripts.vim!" in your runtime directory (same place where filetype.vim goes).
It might look like this:

\begin{Verbatim}[samepage=true]
 if did_filetype()
   finish
 endif
 if getline(1) =~ '^#!.*[/\\]xyz\>'
   setf xyz
 endif
\end{Verbatim}

The first check with \verb!did_filetype()! is to avoid that you will check the contents of files for which the filetype was already detected by the file name.
That avoids wasting time on checking the file when the "\verb!setf!" command won't do anything.

The scripts.vim file is sourced by an autocommand in the default \verb!filetype.vim! file.
Therefore, the order of checks is:

\begin{enumerate}
				\item \verb!filetype.vim! files before \verb!$VIMRUNTIME! in 'runtimepath'
				\item first part of \verb!$VIMRUNTIME/filetype.vim!
				\item all \verb!scripts.vim! files in 'runtimepath'
				\item remainder of \verb!$VIMRUNTIME/filetype.vim!
				\item filetype.vim files after \verb!$VIMRUNTIME! in 'runtimepath'
\end{enumerate}

If this is not sufficient for you, add an autocommand that matches all files and sources a script or executes a function to check the contents of the file.
\clearpage
