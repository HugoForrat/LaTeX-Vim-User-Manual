\section{Your own syntax highlighted}
\label{Your own syntax highlighted}
Vim comes with highlighting for a couple of hundred different file types.
If the file you are editing isn't included, read this chapter to find out how to get this type of file highlighted.
Also see |\verb!:h :syn-define!| in the reference manual.
\localtableofcontentswithrelativedepth{+1}
\subsection{Basic syntax commands}
Using an existing syntax file to start with will save you a lot of time.
Try finding a syntax file in \verb!$VIMRUNTIME/syntax! for a language that is similar.
These files will also show you the normal layout of a syntax file.
To understand it, you need to read the following.

Let's start with the basic arguments.
Before we start defining any new syntax, we need to clear out any old definitions:

\begin{Verbatim}[samepage=true]
 :syntax clear
\end{Verbatim}

This isn't required in the final syntax file, but very useful when experimenting.

There are more simplifications in this chapter.
If you are writing a syntax file to be used by others, read all the way through the end to find out the details.

\subsubsection{Listing defined items}
To check which syntax items are currently defined, use this command:

\begin{Verbatim}[samepage=true]
 :syntax
\end{Verbatim}

You can use this to check which items have actually been defined.
Quite useful when you are experimenting with a new syntax file.
It also shows the colors used for each item, which helps to find out what is what.

To list the items in a specific syntax group use:

\begin{Verbatim}[samepage=true]
 :syntax list {group-name}
\end{Verbatim}

This also can be used to list clusters (explained in |\hyperref[Clusters]{\texttt{Clusters}}|).
Just include the \verb!@! in the name.

\subsubsection{Matching case}
Some languages are not case sensitive, such as Pascal.
Others, such as C, are case sensitive.
You need to tell which type you have with the following commands:

\begin{Verbatim}[samepage=true]
 :syntax case match
 :syntax case ignore
\end{Verbatim}

The "\verb!match!" argument means that Vim will match the case of syntax elements.
Therefore, "\verb!int!" differs from "\verb!Int!" and "\verb!INT!".
If the "\verb!ignore!" argument is used, the following are equivalent: "\verb!Procedure!", "\verb!PROCEDURE!" and "\verb!procedure!".

The "\verb!:syntax case!" commands can appear anywhere in a syntax file and affect the syntax definitions that follow.
In most cases, you have only one ":syntax case" command in your syntax file; if you work with an unusual language that contains both case-sensitive and non-case-sensitive elements, however, you can scatter the "\verb!:syntax case!" command throughout the file.
\subsection{Keywords}
The most basic syntax elements are keywords.
To define a keyword, use the following form:

\begin{Verbatim}[samepage=true]
 :syntax keyword {group} {keyword} ...
\end{Verbatim}

The {group} is the name of a syntax group.
With the "\verb!:highlight!" command you can assign colors to a {group}.
The {keyword} argument is an actual keyword.
Here are a few examples:

\begin{Verbatim}[samepage=true]
 :syntax keyword xType int long char
 :syntax keyword xStatement if then else endif
\end{Verbatim}

This example uses the group names "\verb!xType!" and "\verb!xStatement!".
By convention, each group name is prefixed by the filetype for the language being defined.
This example defines syntax for the x language (eXample language without an interesting name).
In a syntax file for "\verb!csh!" scripts the name "\verb!cshType!" would be used.
Thus the prefix is equal to the value of 'filetype'.

These commands cause the words "\verb!int!", "\verb!long!" and "\verb!char!" to be highlighted one way and the words "\verb!if!", "\verb!then!", "\verb!else!" and "\verb!endif!" to be highlighted another way.
Now you need to connect the x group names to standard Vim names.
You do this with the following commands:

\begin{Verbatim}[samepage=true]
 :highlight link xType Type
 :highlight link xStatement Statement
\end{Verbatim}

This tells Vim to highlight "\verb!xType!" like "\verb!Type!" and "\verb!xStatement!" like "\verb!Statement!".
See |\verb!:h group-name!| for the standard names.

\subsubsection{Unusual keywords}
The characters used in a keyword must be in the 'iskeyword' option.
If you use another character, the word will never match.
Vim doesn't give a warning message for this.

The x language uses the '-' character in keywords.
This is how it's done:

\begin{Verbatim}[samepage=true]
 :setlocal iskeyword+=-
 :syntax keyword xStatement when-not
\end{Verbatim}

The "\verb!:setlocal!" command is used to change 'iskeyword' only for the current buffer.
Still it does change the behavior of commands like "\verb!w!" and "\verb!*!".
If that is not wanted, don't define a keyword but use a match (explained in the next section).

The x language allows for abbreviations.
For example, "\verb!next!" can be abbreviated to "\verb!n!", "\verb!ne!" or "\verb!nex!".
You can define them by using this command:

\begin{Verbatim}[samepage=true]
 :syntax keyword xStatement n[ext]
\end{Verbatim}

This doesn't match "\verb!nextone!", keywords always match whole words only.
\subsection{Matches}
Consider defining something a bit more complex.
You want to match ordinary identifiers.
To do this, you define a match syntax item.
This one matches any word consisting of only lowercase letters:

\begin{Verbatim}[samepage=true]
 :syntax match xIdentifier /\<\l\+\>/
\end{Verbatim}
 
\emph{Note}: Keywords overrule any other syntax item.
Thus the keywords "\verb!if!", "\verb!then!", etc., will be keywords, as defined with the "\verb!:syntax keyword!" commands above, even though they also match the pattern for xIdentifier.

The part at the end is a pattern, like it's used for searching.
The \verb!//! is used to surround the pattern (like how it's done in a "\verb!:substitute!" command).
You can use any other character, like a plus or a quote.

Now define a match for a comment.
In the x language it is anything from \verb!#! to the end of a line:

\begin{Verbatim}[samepage=true]
 :syntax match xComment /#.*/
\end{Verbatim}

Since you can use any search pattern, you can highlight very complex things with a match item.
See |\verb!:h pattern!| for help on search patterns.
\subsection{Regions}

In the example x language, strings are enclosed in double quotation marks (").
To highlight strings you define a region.
You need a region start (double quote) and a region end (double quote).
The definition is as follows:

\begin{Verbatim}[samepage=true]
 :syntax region xString start=/"/ end=/"/
\end{Verbatim}

The "\verb!start!" and "\verb!end!" directives define the patterns used to find the start and end of the region.
But what about strings that look like this?

\begin{Verbatim}[samepage=true]
    "A string with a double quote (\") in it" 
\end{Verbatim}

This creates a problem: The double quotation marks in the middle of the string will end the region.
You need to tell Vim to skip over any escaped double quotes in the string.
Do this with the skip keyword:

\begin{Verbatim}[samepage=true]
 :syntax region xString start=/"/ skip=/\\"/ end=/"/
\end{Verbatim}

The double backslash matches a single backslash, since the backslash is a special character in search patterns.

When to use a region instead of a match?
The main difference is that a match item is a single pattern, which must match as a whole.
A region starts as soon as the "\verb!start!" pattern matches.
Whether the "\verb!end!" pattern is found or not doesn't matter.
Thus when the item depends on the "\verb!end!" pattern to match, you cannot use a region.
Otherwise, regions are often simpler to define.
And it is easier to use nested items, as is explained in the next section.
\subsection{Nested items}
Take a look at this comment:

\begin{Verbatim}[samepage=true]
    %Get input  TODO: Skip white space 
\end{Verbatim}

You want to highlight TODO in big yellow letters, even though it is in a comment that is highlighted blue.
To let Vim know about this, you define the following syntax groups:

\begin{Verbatim}[samepage=true]
 :syntax keyword xTodo TODO contained
 :syntax match xComment /%.*/ contains=xTodo
\end{Verbatim}

In the first line, the "\verb!contained!" argument tells Vim that this keyword can exist only inside another syntax item.
The next line has "\verb!contains=xTodo!".
This indicates that the xTodo syntax element is inside it.
The result is that the comment line as a whole is matched with "\verb!xComment!" and made blue.
The word TODO inside it is matched by xTodo and highlighted yellow (highlighting for xTodo was setup for this).

\subsubsection{Recursive nesting}
The x language defines code blocks in curly braces.
And a code block may contain other code blocks.
This can be defined this way:

\begin{Verbatim}[samepage=true]
 :syntax region xBlock start=/{/ end=/}/ contains=xBlock
\end{Verbatim}

Suppose you have this text:

\begin{Verbatim}[samepage=true]
    while i < b { 
        if a { 
            b = c; 
        } 
    } 
\end{Verbatim}

First a xBlock starts at the \verb!{! in the first line.
In the second line another \verb!{! is found.
Since we are inside a xBlock item, and it contains itself, a nested xBlock item will start here.
Thus the "\verb!b = c!" line is inside the second level xBlock region.
Then a \verb!}! is found in the next line, which matches with the end pattern of the region.
This ends the nested xBlock.
Because the \verb!}! is included in the nested region, it is hidden from the first xBlock region.
Then at the last \verb!}! the first xBlock region ends.

\subsubsection{Keeping the end}
Consider the following two syntax items:

\begin{Verbatim}[samepage=true]
 :syntax region xComment start=/%/ end=/$/ contained
 :syntax region xPreProc start=/#/ end=/$/ contains=xComment
\end{Verbatim}

You define a comment as anything from \verb!%! to the end of the line.
A preprocessor directive is anything from \verb!#! to the end of the line.
Because you can have a comment on a preprocessor line, the preprocessor definition includes a "\verb!contains=xComment!" argument.
Now look what happens with this text:

\begin{Verbatim}[samepage=true]
    #define X = Y  % Comment text 
    int foo = 1; 
\end{Verbatim}

What you see is that the second line is also highlighted as xPreProc.
The preprocessor directive should end at the end of the line.
That is why you have used "\verb!end=/$/!".
So what is going wrong?

The problem is the contained comment.
The comment starts with \verb!%! and ends at the end of the line.
After the comment ends, the preprocessor syntax continues.
This is after the end of the line has been seen, so the next line is included as well.

To avoid this problem and to avoid a contained syntax item eating a needed end of line, use the "\verb!keepend!" argument.
This takes care of the double end-of-line matching:

\begin{Verbatim}[samepage=true]
 :syntax region xComment start=/%/ end=/$/ contained
 :syntax region xPreProc start=/#/ end=/$/ contains=xComment keepend
\end{Verbatim}

\subsubsection{Containing many items}
You can use the contains argument to specify that everything can be contained.
For example:

\begin{Verbatim}[samepage=true]
 :syntax region xList start=/\[/ end=/\]/ contains=ALL
\end{Verbatim}

All syntax items will be contained in this one.
It also contains itself, but not at the same position (that would cause an endless loop).

You can specify that some groups are not contained.
Thus contain all groups but the ones that are listed:

\begin{Verbatim}[samepage=true]
 :syntax region xList start=/\[/ end=/\]/ contains=ALLBUT,xString
\end{Verbatim}

With the "\verb!TOP!" item you can include all items that don't have a "\verb!contained!" argument.
"\verb!CONTAINED!" is used to only include items with a "\verb!contained!" argument.
See |\verb!:h :syn-contains!| for the details.
\subsection{Following groups}
The x language has statements in this form:

\begin{Verbatim}[samepage=true]
    if (condition) then 
\end{Verbatim}

You want to highlight the three items differently.
But "\verb!(condition)!" and "\verb!then!" might also appear in other places, where they get different highlighting.
This is how you can do this:

\begin{Verbatim}[samepage=true]
 :syntax match xIf /if/ nextgroup=xIfCondition skipwhite
 :syntax match xIfCondition /([^)]*)/ contained nextgroup=xThen skipwhite
 :syntax match xThen /then/ contained
\end{Verbatim}

The "\verb!nextgroup!" argument specifies which item can come next.
This is not required.
If none of the items that are specified are found, nothing happens.
For example, in this text:

\begin{Verbatim}[samepage=true]
    if not (condition) then 
\end{Verbatim}

The "\verb!if!" is matched by xIf.
"\verb!not!" doesn't match the specified nextgroup xIfCondition, thus only the "\verb!if!" is highlighted.

The "\verb!skipwhite!" argument tells Vim that white space (spaces and tabs) may appear in between the items.
Similar arguments are "\verb!skipnl!", which allows a line break in between the items, and "\verb!skipempty!", which allows empty lines.
Notice that "\verb!skipnl!" doesn't skip an empty line, something must match after the line break.
\subsection{Other arguments}
\subsubsection{Matchgroup}
When you define a region, the entire region is highlighted according to the group name specified.
To highlight the text enclosed in parentheses \verb!()! with the group xInside, for example, use the following command:

\begin{Verbatim}[samepage=true]
 :syntax region xInside start=/(/ end=/)/
\end{Verbatim}

Suppose, that you want to highlight the parentheses differently.
You can do this with a lot of convoluted region statements, or you can use the "\verb!matchgroup!" argument.
This tells Vim to highlight the start and end of a region with a different highlight group (in this case, the xParen group):

\begin{Verbatim}[samepage=true]
 :syntax region xInside matchgroup=xParen start=/(/ end=/)/
\end{Verbatim}

The "\verb!matchgroup!" argument applies to the start or end match that comes after it.
In the previous example both start and end are highlighted with xParen.
To highlight the end with xParenEnd:

\begin{Verbatim}[samepage=true]
 :syntax region xInside matchgroup=xParen start=/(/
     \ matchgroup=xParenEnd end=/)/
\end{Verbatim}

A side effect of using "\verb!matchgroup!" is that contained items will not match in the start or end of the region.
The example for "transparent" uses this.

\subsubsection{Transparent}
In a C language file you would like to highlight the \verb!()! text after a "\verb!while!" differently from the \verb!()! text after a "\verb!for!".
In both of these there can be nested \verb!()! items, which should be highlighted in the same way.
You must make sure the \verb!()! highlighting stops at the matching \verb!)!.
This is one way to do this:

\begin{Verbatim}[samepage=true]
 :syntax region cWhile matchgroup=cWhile start=/while\s*(/ end=/)/
     \ contains=cCondNest
 :syntax region cFor matchgroup=cFor start=/for\s*(/ end=/)/
     \ contains=cCondNest
 :syntax region cCondNest start=/(/ end=/)/ contained transparent
\end{Verbatim}

Now you can give \verb!cWhile! and \verb!cFor! different highlighting.
The cCondNest item can appear in either of them, but take over the highlighting of the item it is contained in.
The "\verb!transparent!" argument causes this.

Notice that the "\verb!matchgroup!" argument has the same group as the item itself.
Why define it then?  Well, the side effect of using a matchgroup is that contained items are not found in the match with the start item then.
This avoids that the \verb!cCondNest! group matches the \verb!(! just after the "\verb!while!" or "\verb!for!".
If this would happen, it would span the whole text until the matching \verb!)! and the region would continue after it.
Now \verb!cCondNest! only matches after the match with the start pattern, thus after the first \verb!(!.

\subsubsection{Offsets}
Suppose you want to define a region for the text between \verb!(! and \verb!)! after an "\verb!if!".
But you don't want to include the "\verb!if!" or the \verb!(! and \verb!)!.
You can do this by specifying offsets for the patterns.
Example:

\begin{Verbatim}[samepage=true]
 :syntax region xCond start=/if\s*(/ms=e+1 end=/)/me=s-1
\end{Verbatim}

The offset for the start pattern is "\verb!ms=e+1!".
"\verb!ms!" stands for Match Start.
This defines an offset for the start of the match.
Normally the match starts where the pattern matches.
"\verb!e+1!" means that the match now starts at the end of the pattern match, and then one character further.

The offset for the end pattern is "\verb!me=s-1!".
"\verb!me!" stands for Match End.
"\verb!s-1!" means the start of the pattern match and then one character back.
The result is that in this text:

\begin{Verbatim}[samepage=true]
    if (foo == bar) 
\end{Verbatim}

Only the text "\verb!foo == bar!" will be highlighted as \verb!xCond!.

More about offsets here: |\verb!:h :syn-pattern-offset!|.
\subsubsection{Oneline}
The "\verb!oneline!" argument indicates that the region does not cross a line boundary.
For example:

\begin{Verbatim}[samepage=true]
 :syntax region xIfThen start=/if/ end=/then/ oneline
\end{Verbatim}

This defines a region that starts at "\verb!if!" and ends at "\verb!then!".
But if there is no "\verb!then!" after the "\verb!if!", the region doesn't match.

\emph{Note}: When using "\verb!oneline!" the region doesn't start if the end pattern doesn't match in the same line.
Without "\verb!oneline!" Vim does \textbf{not} check if there is a match for the end pattern.
The region starts even when the end pattern doesn't match in the rest of the file.

\subsubsection{Continuation lines and avoiding them}
Things now become a little more complex.
Let's define a preprocessor line.
This starts with a \verb!#! in the first column and continues until the end of the line.
A line that ends with \verb!\! makes the next line a continuation line.
The way you handle this is to allow the syntax item to contain a continuation pattern:

\begin{Verbatim}[samepage=true]
 :syntax region xPreProc start=/^#/ end=/$/ contains=xLineContinue
 :syntax match xLineContinue "\\$" contained
\end{Verbatim}

In this case, although \verb!xPreProc! normally matches a single line, the group contained in it (namely \verb!xLineContinue!) lets it go on for more than one line.
For example, it would match both of these lines:

\begin{Verbatim}[samepage=true]
    #define SPAM  spam spam spam \ 
            bacon and spam 
\end{Verbatim}

In this case, this is what you want.
If it is not what you want, you can call for the region to be on a single line by adding "\verb!excludenl!" to the contained pattern.
For example, you want to highlight "\verb!end!" in \verb!xPreProc!, but only at the end of the line.
To avoid making the \verb!xPreProc! continue on the next line, like xLineContinue does, use "\verb!excludenl!" like this:

\begin{Verbatim}[samepage=true]
 :syntax region xPreProc start=/^#/ end=/$/
     \ contains=xLineContinue,xPreProcEnd
 :syntax match xPreProcEnd excludenl /end$/ contained
 :syntax match xLineContinue "\\$" contained
\end{Verbatim}

"\verb!excludenl!" must be placed before the pattern.
Since "\verb!xLineContinue!" doesn't have "\verb!excludenl!", a match with it will extend \verb!xPreProc! to the next line as before.
\subsection{Clusters}
\label{Clusters}
One of the things you will notice as you start to write a syntax file is that you wind up generating a lot of syntax groups.
Vim enables you to define a collection of syntax groups called a cluster.

Suppose you have a language that contains for loops, if statements, while loops, and functions.
Each of them contains the same syntax elements: numbers and identifiers.
You define them like this:

\begin{Verbatim}[samepage=true]
 :syntax match xFor /^for.*/ contains=xNumber,xIdent
 :syntax match xIf /^if.*/ contains=xNumber,xIdent
 :syntax match xWhile /^while.*/ contains=xNumber,xIdent
\end{Verbatim}

You have to repeat the same "\verb!contains=!" every time.
If you want to add another contained item, you have to add it three times.
Syntax clusters simplify these definitions by enabling you to have one cluster stand for several syntax groups.

To define a cluster for the two items that the three groups contain, use the following command:

\begin{Verbatim}[samepage=true]
 :syntax cluster xState contains=xNumber,xIdent
\end{Verbatim}

Clusters are used inside other syntax items just like any syntax group.
Their names start with @.
Thus, you can define the three groups like this:

\begin{Verbatim}[samepage=true]
 :syntax match xFor /^for.*/ contains=@xState
 :syntax match xIf /^if.*/ contains=@xState
 :syntax match xWhile /^while.*/ contains=@xState
\end{Verbatim}

You can add new group names to this cluster with the "\verb!add!" argument:

\begin{Verbatim}[samepage=true]
 :syntax cluster xState add=xString
\end{Verbatim}

You can remove syntax groups from this list as well:

\begin{Verbatim}[samepage=true]
 :syntax cluster xState remove=xNumber
\end{Verbatim}

\subsection{Including another syntax file}
The C++ language syntax is a superset of the C language.
Because you do not want to write two syntax files, you can have the C++ syntax file read in the one for C by using the following command:

\begin{Verbatim}[samepage=true]
 :runtime! syntax/c.vim
\end{Verbatim}

The "\verb!:runtime!!" command searches 'runtimepath' for all "\verb!syntax/c.vim!" files.
This makes the C parts of the C++ syntax be defined like for C files.
If you have replaced the \verb!c.vim! syntax file, or added items with an extra file, these will be loaded as well.

After loading the C syntax items the specific C++ items can be defined.
For example, add keywords that are not used in C:

\begin{Verbatim}[samepage=true]
 :syntax keyword cppStatement    new delete this friend using
\end{Verbatim}

This works just like in any other syntax file.

Now consider the Perl language.
A Perl script consists of two distinct parts: a documentation section in POD format, and a program written in Perl itself.
The POD section starts with "\verb!=head!" and ends with "\verb!=cut!".

You want to define the POD syntax in one file, and use it from the Perl syntax file.
The "\verb!:syntax include!" command reads in a syntax file and stores the elements it defined in a syntax cluster.
For Perl, the statements are as follows:

\begin{Verbatim}[samepage=true]
 :syntax include @Pod <sfile>:p:h/pod.vim
 :syntax region perlPOD start=/^=head/ end=/^=cut/ contains=@Pod
\end{Verbatim}

When "\verb!=head!" is found in a Perl file, the perlPOD region starts.
In this region the @Pod cluster is contained.
All the items defined as top-level items in the pod.vim syntax files will match here.
When "\verb!=cut!" is found, the region ends and we go back to the items defined in the Perl file.

The "\verb!:syntax include!" command is clever enough to ignore a "\verb!:syntax clear!" command in the included file.
And an argument such as "\verb!contains=ALL!" will only contain items defined in the included file, not in the file that includes it.

The "\verb!<sfile>:p:h/!" part uses the name of the current file (<sfile>), expands it to a full path (:p) and then takes the head (:h).
This results in the directory name of the file.
This causes the pod.vim file in the same directory to be included.
\subsection{Synchronizing}
Compilers have it easy.
They start at the beginning of a file and parse it straight through.
Vim does not have it so easy.
It must start in the middle, where the editing is being done.
So how does it tell where it is?

The secret is the "\verb!:syntax sync!" command.
This tells Vim how to figure out where it is.
For example, the following command tells Vim to scan backward for the beginning or end of a C-style comment and begin syntax coloring from there:

\begin{Verbatim}[samepage=true]
 :syntax sync ccomment
\end{Verbatim}

You can tune this processing with some arguments.
The "\verb!minlines!" argument tells Vim the minimum number of lines to look backward, and "\verb!maxlines!" tells the editor the maximum number of lines to scan.

For example, the following command tells Vim to look at least 10 lines before the top of the screen:

\begin{Verbatim}[samepage=true]
 :syntax sync ccomment minlines=10 maxlines=500
\end{Verbatim}

If it cannot figure out where it is in that space, it starts looking farther and farther back until it figures out what to do.
But it looks no farther back than 500 lines.
(A large "\verb!maxlines!" slows down processing.
A small one might cause synchronization to fail.)

To make synchronizing go a bit faster, tell Vim which syntax items can be skipped.
Every match and region that only needs to be used when actually displaying text can be given the "\verb!display!" argument.

By default, the comment to be found will be colored as part of the Comment syntax group.
If you want to color things another way, you can specify a different syntax group:

\begin{Verbatim}[samepage=true]
 :syntax sync ccomment xAltComment
\end{Verbatim}

If your programming language does not have C-style comments in it, you can try another method of synchronization.
The simplest way is to tell Vim to space back a number of lines and try to figure out things from there.
The following command tells Vim to go back 150 lines and start parsing from there:

\begin{Verbatim}[samepage=true]
 :syntax sync minlines=150
\end{Verbatim}

A large "\verb!minlines!" value can make Vim slower, especially when scrolling backwards in the file.

Finally, you can specify a syntax group to look for by using this command:

\begin{Verbatim}[samepage=true]
 :syntax sync match {sync-group-name}
     \ grouphere {group-name} {pattern}
\end{Verbatim}

This tells Vim that when it sees {pattern} the syntax group named {group-name} begins just after the pattern given.
The {sync-group-name} is used to give a name to this synchronization specification.
For example, the sh scripting language begins an if statement with "\verb!if!" and ends it with "\verb!fi!":

\begin{Verbatim}[samepage=true]
    if [ --f file.txt ] ; then 
        echo "File exists" 
    fi 
\end{Verbatim}

To define a "\verb!grouphere!" directive for this syntax, you use the following command:

\begin{Verbatim}[samepage=true]
 :syntax sync match shIfSync grouphere shIf "\<if\>"
\end{Verbatim}

The "\verb!groupthere!" argument tells Vim that the pattern ends a group.
For example, the end of the if/fi group is as follows:

\begin{Verbatim}[samepage=true]
 :syntax sync match shIfSync groupthere NONE "\<fi\>"
\end{Verbatim}

In this example, the \verb!NONE! tells Vim that you are not in any special syntax region.
In particular, you are not inside an if block.

You also can define matches and regions that are with no "\verb!grouphere!" or "\verb!groupthere!" arguments.
These groups are for syntax groups skipped during synchronization.
For example, the following skips over anything inside {}, even if it would normally match another synchronization method:

\begin{Verbatim}[samepage=true]
 :syntax sync match xSpecial /{.*}/
\end{Verbatim}

More about synchronizing in the reference manual: |\verb!:h :syn-sync!|.
\subsection{Installing a syntax file}
When your new syntax file is ready to be used, drop it in a "\verb!syntax!" directory in 'runtimepath'.
For Unix that would be "\verb!~/.vim/syntax!".
The name of the syntax file must be equal to the file type, with "\verb!.vim!" added.
Thus for the x language, the full path of the file would be:

\begin{Verbatim}[samepage=true]
    ~/.vim/syntax/x.vim 
\end{Verbatim}

You must also make the file type be recognized.
See |\hyperref[Adding a filetype]{\texttt{Adding a filetype}}|.

If your file works well, you might want to make it available to other Vim users.
First read the next section to make sure your file works well for others. Then e-mail it to the Vim maintainer: <maintainer@vim.org>. Also explain how the filetype can be detected.
With a bit of luck your file will be included in the next Vim version!

\subsubsection{Adding to an existing syntax file}
We were assuming you were adding a completely new syntax file.
When an existing syntax file works, but is missing some items, you can add items in a separate file.
That avoids changing the distributed syntax file, which will be lost when installing a new version of Vim.

Write syntax commands in your file, possibly using group names from the existing syntax.
For example, to add new variable types to the C syntax file:

\begin{Verbatim}[samepage=true]
 :syntax keyword cType off_t uint
\end{Verbatim}

Write the file with the same name as the original syntax file.
In this case "\verb!c.vim!".
Place it in a directory near the end of 'runtimepath'.
This makes it loaded after the original syntax file.
For Unix this would be:

\begin{Verbatim}[samepage=true]
    ~/.vim/after/syntax/c.vim 
\end{Verbatim}
\subsection{Portable syntax file layout}
Wouldn't it be nice if all Vim users exchange syntax files?  To make this possible, the syntax file must follow a few guidelines.

Start with a header that explains what the syntax file is for, who maintains it and when it was last updated.
Don't include too much information about changes history, not many people will read it.
Example:

\begin{Verbatim}[samepage=true]
 " Vim syntax file
 " Language: C
 " Maintainer:   Bram Moolenaar <Bram@vim.org>
 " Last Change:  2001 Jun 18
 " Remark:   Included by the C++ syntax.
\end{Verbatim}

Use the same layout as the other syntax files.
Using an existing syntax file as an example will save you a lot of time.

Choose a good, descriptive name for your syntax file.
Use lowercase letters and digits.
Don't make it too long, it is used in many places: The name of the syntax file "\verb!name.vim!", 'filetype', \verb!b:current_syntax! and the start of each syntax group (nameType, nameStatement, nameString, etc).

Start with a check for "\verb!b:current_syntax!".
If it is defined, some other syntax file, earlier in 'runtimepath' was already loaded:

\begin{Verbatim}[samepage=true]
 if exists("b:current_syntax")
   finish
 endif
\end{Verbatim}

To be compatible with Vim 5.8 use:

\begin{Verbatim}[samepage=true]
 if version < 600
   syntax clear
 elseif exists("b:current_syntax")
   finish
 endif
\end{Verbatim}

Set "\verb!b:current_syntax!" to the name of the syntax at the end.
Don't forget that included files do this too, you might have to reset "\verb!b:current_syntax!" if you include two files.

If you want your syntax file to work with Vim 5.x, add a check for \verb!v:version!.
See \verb!yacc.vim! for an example.

Do not include anything that is a user preference.
Don't set 'tabstop', 'expandtab', etc.
These belong in a filetype plugin.

Do not include mappings or abbreviations.
Only include setting 'iskeyword' if it is really necessary for recognizing keywords.

To allow users select their own preferred colors, make a different group name for every kind of highlighted item.
Then link each of them to one of the standard highlight groups.
That will make it work with every color scheme.
If you select specific colors it will look bad with some color schemes.
And don't forget that some people use a different background color, or have only eight colors available.

For the linking use "\verb!hi def link!", so that the user can select different highlighting before your syntax file is loaded.
Example:

\begin{Verbatim}[samepage=true]
   hi def link nameString    String
   hi def link nameNumber    Number
   hi def link nameCommand   Statement
   ... etc ...
\end{Verbatim}

Add the "\verb!display!" argument to items that are not used when syncing, to speed up scrolling backwards and CTRL-L.
\clearpage
